plugins {
    id 'java-library'
    id 'org.springframework.boot'
}

// API 모듈은 모든 도메인 모듈 의존
dependencies {
    implementation project(':ticketmate-admin')
    implementation project(':ticketmate-ai')
    implementation project(':ticketmate-applicationform')
    implementation project(':ticketmate-auth')
    implementation project(':ticketmate-chat')
    implementation project(':ticketmate-common')
    implementation project(':ticketmate-concert')
    implementation project(':ticketmate-concerthall')
    implementation project(':ticketmate-member')
    implementation project(':ticketmate-messaging')
    implementation project(':ticketmate-mock')
    implementation project(':ticketmate-notification')
    implementation project(':ticketmate-portfolio')
    implementation project(':ticketmate-querydsl')
    implementation project(':ticketmate-redis')
    implementation project(':ticketmate-search')
    implementation project(':ticketmate-sms')
    implementation project(':ticketmate-storage')
    implementation project(':ticketmate-totp')
    implementation project(':ticketmate-websocket')

    // Swagger UI
    implementation libs.springdoc.openapi.ui

    // Chuseok22 API Change Log
    implementation libs.api.changelog

    // Flyway
    implementation libs.flyway.core
    implementation libs.flyway.postgres
}

tasks.register('schemaVerifyTest', Test) {
    group = 'verification'
    description = 'Run schema verification context only (Flyway migrate + JPA validate + DDL snapshot)'
    useJUnitPlatform()

    // 스키마 검증용 얇은 테스트 클래스만 실행
    include '**/SchemaValidationTest.class'

    // 프로파일 강제 (워크플로우/로컬 모두 동일하게 동작)
    systemProperty 'spring.profiles.active', 'schema-verify'

    // 보기 좋은 로그
    testLogging {
        events 'failed', 'skipped'
        exceptionFormat 'full'
        showCauses true
        showExceptions true
        showStackTraces true
    }
}
